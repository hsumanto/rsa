apply plugin: 'java'
apply plugin: 'application'

import java.nio.file.Files
import java.nio.file.Paths

mainClassName = 'org.vpac.ndg.cli.Client'

dependencies {
    compile (
        'commons-cli:commons-cli:1.3.1',
        'commons-io:commons-io:1.3.2',
        'com.martiansoftware:nailgun-examples:0.9.1',
        'org.jsoup:jsoup:1.6.1',
        "org.springframework:org.springframework.web:$springVersion",
        project(':storagemanager'),
        project(':rsa-common'),
    )

    testCompile (
        'com.carrotsearch:junit-benchmarks:0.4.0',
        "org.springframework:org.springframework.test:$springVersion",
        sourceSets.main.output,
    )
}

jar {
    manifest {
        attributes("Implementation-Title": applicationName)
    }
}

startScripts {
    // Workaround for application plugin setting the wrong classpath for the
    // config directory: It's in src/dist/config, which gets copied to
    // APP_HOME/config.
    // https://issues.gradle.org/browse/GRADLE-2333
    // http://stackoverflow.com/a/18712556/320036
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replaceFirst(
            /%APP_HOME%\\lib\\config(:|$)/,
            '%APP_HOME%\\config$1')
        unixScriptFile.text  = unixScriptFile.text.replaceFirst(
            /APP_HOME\/lib\/config(:|$)/,
            'APP_HOME/config$1')
    }
}

installDist << {
    // Create a symlink to the primary config directory. An alternative is to
    // create a symlink as src/dist/config, but in that case the contents of
    // the directory get *copied* to the output directory, which makes
    // overriding config (e.g. with a Docker volume) difficult.
    delete "$buildDir/install/$applicationName/config"
    def link = Paths.get("$buildDir/install/$applicationName/config")
    def target = Paths.get("$rootDir/../config").normalize()
    Files.createSymbolicLink link, target
}

// Prevent the contents of the config directory from being included in the
// lib directory.
applicationDistribution.with {
    exclude 'config/**'
}
