apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'org.vpac.ndg.cli.Client'

dependencies {
    compile (
        'commons-cli:commons-cli:1.2',
        'commons-io:commons-io:1.3.2',
        'com.martiansoftware:nailgun-examples:0.9.1',
        'org.jsoup:jsoup:1.6.1',
        'org.springframework:org.springframework.web:3.1.1.RELEASE',
        project(':storagemanager'),
    )

    testCompile (
        'com.carrotsearch:junit-benchmarks:0.4.0',
        'org.springframework:org.springframework.test:3.1.1.RELEASE',
        sourceSets.main.output,
    )
}

jar {
    manifest {
        attributes("Implementation-Title": "rsaworkers")
    }
}

startScripts {
    // Workaround for application plugin setting the wrong classpath for the
    // config directory: It's in src/dist/config, which gets copied to
    // APP_HOME/config.
    // https://issues.gradle.org/browse/GRADLE-2333
    // http://stackoverflow.com/a/18712556/320036
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replaceFirst(
            /%APP_HOME%\\lib\\config(:|$)/,
            '%APP_HOME%\\config$1')
        unixScriptFile.text  = unixScriptFile.text.replaceFirst(
            /APP_HOME\/lib\/config(:|$)/,
            'APP_HOME/config$1')
    }
}
